plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    alias(libs.plugins.kotlin.jvm)

    // Apply the detekt static analysis plugin for Kotlin code.
    // See: https://detekt.dev/docs/1.23.8/gettingstarted/gradle
    id("io.gitlab.arturbosch.detekt") version "1.23.8"

    // Apply Dokka for automatic documentation generation
    id("org.jetbrains.dokka") version "2.1.0"

    // Apply the JaCoCo plugin for code coverage reports.
    jacoco

    // Apply maven-publish plugin for library publishing
    `maven-publish`
}

import org.gradle.api.publish.maven.tasks.PublishToMavenRepository

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // AWS SDK for Kotlin
    implementation("aws.sdk.kotlin:dynamodb:1.5.113")

    // JUnit Jupiter for testing.
    testImplementation(libs.junit.jupiter)
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    // Mockito Kotlin for mocking in tests.
    // See: https://github.com/mockito/mockito-kotlin/wiki/Mocking-and-verifying
    testImplementation("org.mockito.kotlin:mockito-kotlin:6.1.0")

    // Testcontainers for local integration testing.
    // See: https://java.testcontainers.org/quickstart/junit_5_quickstart/
    testImplementation("org.testcontainers:localstack:1.21.4")
    testImplementation("org.testcontainers:testcontainers:2.0.3")
    testImplementation("org.testcontainers:testcontainers-junit-jupiter:2.0.3")

    // Detekt dependencies for static code analysis.
    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:1.23.8")
}

kotlin {
    jvmToolchain(21)
}

jacoco {
    toolVersion = "0.8.10"
}

detekt {
    // Version of detekt to use. For a list of available versions,
    // see https://github.com/detekt/detekt/releases.
    toolVersion = "1.23.8"

    // Directory where detekt will search for source files.
    source.setFrom("src/main/kotlin", "src/test/kotlin", "src/localIntegTest/kotlin")

    // Specify custom detekt config file for overriding lint rules.
    config.setFrom("$projectDir/config/detekt.yml")

    // Builds the AST in parallel, can speed up builds in larger projects.
    parallel = true

    // Adds debug output during task execution. `false` by default.
    debug = false
}

tasks.test {
    useJUnitPlatform {
        excludeTags("localIntegTest")
    }

    finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
    reports {
        html.required.set(true)
    }
}

tasks.jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = "0.95".toBigDecimal()
            }
        }
    }
}

tasks.check {
    dependsOn(tasks.jacocoTestCoverageVerification)
}

tasks.withType<io.gitlab.arturbosch.detekt.Detekt>().configureEach {
    jvmTarget = "21"

    reports {
        html.required.set(true) // observe findings in your browser with structure and code snippets
        txt.required.set(true) // similar to the console output, contains issue signature to manually edit baseline files
    }
}

tasks.withType<io.gitlab.arturbosch.detekt.DetektCreateBaselineTask>().configureEach {
    jvmTarget = "21"
}

// ------------------------------
// Local integration test config
// ------------------------------

// Define source set for local integ tests
val localIntegTestSourceSet = sourceSets.create("localIntegTest") {
    kotlin.srcDir("src/test/kotlin/com/ondemanddeletionplatform/deletion/localinteg")
    compileClasspath += sourceSets.main.get().output
    runtimeClasspath += sourceSets.main.get().output
}

// Reuse test dependencies for local integ tests
configurations["localIntegTestImplementation"]
    .extendsFrom(configurations["testImplementation"])
configurations["localIntegTestRuntimeOnly"]
    .extendsFrom(configurations["testRuntimeOnly"])

// Define task to run local integ tests
val localIntegTest = tasks.register<Test>("localIntegTest") {
    description = "Runs local integration tests."
    group = "verification"

    testClassesDirs = localIntegTestSourceSet.output.classesDirs
    classpath = localIntegTestSourceSet.runtimeClasspath

    useJUnitPlatform {
        includeTags("localIntegTest")
    }

    shouldRunAfter(tasks.test)

    onlyIf {
        System.getProperty("runLocalIntegTests") == "true"
    }
}

tasks.build {
    // Ensure localIntegTest compilation is included in build task
    dependsOn(tasks.named("compileLocalIntegTestKotlin"))

    // Auto-generate documentation during builds
    dependsOn(tasks.dokkaGenerateHtml)
}

// Library publishing config
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/On-Demand-Deletion-Platform/aws-data-deletion-sdk")
            credentials {
                username = project.findProperty("gpr.user") as String? ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") as String? ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        create<MavenPublication>("maven") {
            from(components["kotlin"])

            groupId = "com.ondemanddeletionplatform"
            artifactId = "aws-data-deletion-sdk"
            version = "0.0.2"

            pom {
                name.set("AWS Data Deletion SDK")
                description.set("Libraries and data models for executing data deletion requests against onboarded AWS-hosted data stores")
                url.set("https://github.com/On-Demand-Deletion-Platform/aws-data-deletion-sdk")

                licenses {
                    license {
                        name.set("GPL-3.0")
                        url.set("https://www.gnu.org/licenses/gpl-3.0.html")
                    }
                }

                scm {
                    connection.set("scm:git:git://github.com/On-Demand-Deletion-Platform/aws-data-deletion-sdk.git")
                    developerConnection.set("scm:git:ssh://github.com/On-Demand-Deletion-Platform/aws-data-deletion-sdk.git")
                    url.set("https://github.com/On-Demand-Deletion-Platform/aws-data-deletion-sdk")
                }
            }
        }
    }
}

tasks.withType<PublishToMavenRepository>().configureEach {
    notCompatibleWithConfigurationCache("Publishing tasks are not compatible with configuration cache")
}
